package cn.tuyucheng.taketoday.kogito.rules

unit LoanApplicationUnit

import cn.tuyucheng.taketoday.kogito.rules.model.LoanApplication
import cn.tuyucheng.taketoday.kogito.rules.model.Person
import cn.tuyucheng.taketoday.kogito.rules.model.Conditions
import cn.tuyucheng.taketoday.kogito.rules.model.Decision

rule "only-adults"
  salience 10
  when
    $application: /applications[
      person.age < 18
    ]
  then
    modify($application){
      setDecision(Decision.REJECTED_UNDERAGE)
    }
end

rule "Good Credit Score And High Income"
  when
    $application: /applications[
      person.age >= 18,
      conditions.creditScore >= 700,
      conditions.income > 2*conditions.debt
    ]
  then
    modify($application){
      setDecision(Decision.APPROVED)
    }
end

rule "Low Credit Score Or High Debt"
  when
    $application: /applications[
      person.age >= 18,
      (
        conditions.creditScore < 700 ||
        conditions.income <= 2*conditions.debt
      )
    ]
  then
    modify($application){
      setDecision(Decision.REJECTED_NOT_CREDITWORTHY)
    }
end

query applications
  $result: /applications
end
